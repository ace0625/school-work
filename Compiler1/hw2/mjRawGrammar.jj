// This is supporting software for CS321 Compilers and Language Design I
// Copyright (c) Portland State University
//
//----------------------------------------------------------------------
// miniJava Raw Grammar (JavaCC Specification)
//
// (For CS321 Winter 2015 - Jingke Li)
//----------------------------------------------------------------------
//
//

// options { DEBUG_PARSER=true; }  /* Show debugging info */

PARSER_BEGIN(mjRawGrammar)
import java.io.*;

public class mjRawGrammar {
  public static void main(String [] args) {
    try {
      if (args.length == 1) {
	FileInputStream stream = new FileInputStream(args[0]);
	new mjRawGrammar(stream).Program();
	stream.close();
      } else {
	System.out.println("Need a file name as command-line argument.");
      }
    } catch (TokenMgrError e) {
      System.err.println(e);
    } catch (Exception e) {
      System.err.println(e);
    }
  }
}
PARSER_END(mjRawGrammar)

//
// LEXER SECTION ---------------------------------------------------------------
//

SKIP : /* White space */
{
  " " | "\t" | "\n" | "\r" | "\f"
}

SKIP : /* Comments -- multi-line form and error detection not included */
{
  <"//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")> 
}

TOKEN : /* Keywords */
{
  "class" | "extends" | "static" | "public" | "void" | "int" | "double" 
| "boolean" | "new" | "this" | "if" | "else" | "while" | "return" | "main"
| "true" | "false"  | "String" | "System" | "out" | "println" 
}

TOKEN : /* Internal tokens */
{
  <#DIGIT:  ["0"-"9"]>
| <#LETTER: (["A"-"Z"]|["a"-"z"])>
}

TOKEN : /* Integer literals */
{
  <INTLIT: (<DIGIT>)+> 
  { try { 
      Integer.parseInt(matchedToken.image); 
    } catch (Exception e) { 
      throw new TokenMgrError("Lexical error at line " + matchedToken.beginLine + 
			      ", column " + matchedToken.beginColumn + 
			      ". Integer overflow: " + matchedToken.image, 0);
    } 
  }
}

TOKEN : /* Double literals */
{
  <DBLLIT: (<DIGIT>)+"."(<DIGIT>)*|(<DIGIT>)*"."(<DIGIT>)+> 
  { try { 
      Double.parseDouble(matchedToken.image); 
    } catch (Exception e) { 
      throw new TokenMgrError("Lexical error at line " + matchedToken.beginLine + 
			      ", column " + matchedToken.beginColumn + 
			      ". Malformed double: " + matchedToken.image, 0);
    } 
  }
}

TOKEN : /* String literals */
{
  <STRLIT: ("\"" (~["\"","\n"])* "\"")>
}

TOKEN : /* Identifiers */
{
  <ID: <LETTER> (<LETTER>|<DIGIT>)*>
}

TOKEN : /* Operators and delimiters */
{
   "+" | "-" | "*" | "/" | "&&" | "||" | "!" 
|  "==" | "!=" | "<" | "<=" | ">" | ">="
|  "=" | ";" | "," | "." | "(" | ")" | "[" | "]" | "{" | "}"
}

//
// PARSER SECTION ---------------------------------------------------------------
//

// Program -> {ClassDecl}
//
void Program(): {}
{
  (ClassDecl())* <EOF>
}

// ClassDecl -> "class" <ID> ["extends" <ID>] "{" {VarDecl} {MethodDecl} "}"
//
void ClassDecl(): {}
{
  "class" <ID> ["extends" <ID>] "{" (VarDecl())* (MethodDecl())* "}"
}

// MethodDecl -> "public" ExtType <ID> "(" [Param {"," Param}] ")" 
//                 "{" {VarDecl} {Stmt} "}"
//            |  "public" "static" "void" "main" "(" "String" "[" "]" <ID> ")"
//                 "{" {VarDecl} {Stmt} "}" 
// 
void MethodDecl(): {} 
{
  "public" ExtType() <ID> "(" [Param() ("," Param())*] ")" 
          "{" (VarDecl())* (Stmt())* "}"
| "public" "static" "void" "main" "(" "String" "[" "]" <ID> ")"
          "{" (VarDecl())* (Stmt())* "}"
}

// Param -> Type <ID> 
//
void Param(): {} 
{
  Type() <ID> 
}

// VarDecl -> Type <ID> ["=" InitExpr] ";"
//  
void VarDecl(): {}
{
  Type() <ID> ["=" InitExpr()] ";"
}

// ExtType -> Type | "void"
//
void ExtType(): {}
{
  Type() | "void"
}

// Type -> BasicType 
//      |  BasicType "[" "]" 
//      |  <ID>
//
void Type(): {}
{
  BasicType() 
| BasicType() "[" "]"
| <ID>
}

// BasicType -> "int" | "double" | "boolean"
//
void BasicType(): {}
{
  "int" | "double" | "boolean"
}

// Stmt -> "{" {Stmt} "}"
//      |  ExtId "(" [Args] ")" ";"
//      |  Lvalue "=" InitExpr ";"
//      |  "if" "(" Expr ")" Stmt ["else" Stmt]
//      |  "while" "(" Expr ")" Stmt
//      |  "System" "." "out" "." "println" "(" [PrArg] ")" ";"
//      |  "return" [Expr] ";"
//
void Stmt(): {}
{
  "{" (Stmt())* "}"
| ExtId() "(" [Args()] ")" ";"
| Lvalue() "=" InitExpr() ";"
| "if" "(" Expr() ")" Stmt() ["else" Stmt()]
| "while" "(" Expr() ")" Stmt()
| "System" "." "out" "." "println" "(" [ PrArg() ] ")" ";"  
| "return" [Expr()] ";"
}

// Args -> Expr {"," Expr}
//
void Args(): {}
{
  Expr() ("," Expr())*
}

// PrArg -> Expr | <STRLIT>
//
void PrArg(): {}
{
  Expr() | <STRLIT>
}

// InitExpr -> "new" BasicType "[" <INTLIT> "]" 
//          |  "new" <ID> "(" ")"
//          |  Expr 
//
void InitExpr(): {}
{
  "new" BasicType() "[" <INTLIT> "]" 
| "new" <ID> "(" ")"
| Expr()
}

// Expr -> Expr BinOp Expr 
//      |  UnOp Expr
//      |  "(" Expr ")" 
//      |  ExtId "(" [Args] ")"
//      |  Lvalue
//      |  Literal
//
void Expr(): {}
{
  Expr() BinOp() Expr() 
| UnOp() Expr()
| "(" Expr() ")" 
| ExtId() "(" [Args()] ")"
| Lvalue()
| Literal()
}

// Lvalue -> ExtId "[" Expr "]"
//        |  ExtId 
void Lvalue(): {}
{
  ExtId() "[" Expr() "]"
| ExtId()
}

// ExtId -> ["this" "."] <ID> {"." <ID>} 
//
void ExtId(): {}
{
  ["this" "."] <ID> ("." <ID>)*
}

// Literal -> <INTLIT> | <DBLLIT> | "true" | "false"
//
void Literal(): {}
{
  <INTLIT> | <DBLLIT> | "true" | "false"
}

// BinOp -> "+" | "-" | "*" | "/" | "&&" | "||" 
//       |  "==" | "!=" | "<" | "<=" | ">" | ">=" 
//
void BinOp(): {}
{
  "+"  | "-"  | "*" | "/"  | "&&" | "||" 
| "==" | "!=" | "<" | "<=" | ">"  | ">=" 
}

// UnOp -> "-" | "!"
//
void UnOp(): {}
{
  "-" | "!"
}
